	assume	adl=1

	section	.text

	public	_truncf, _trunc

	private	__truncf_ret_zero
__truncf_ret_zero:
	; return signed zero
	ld	a, e
	and	a, $80
	ld	e, a
	sbc	hl, hl
	ret

; Warning: _modff assumes that this routine preserves IY
_trunc:
_truncf:
	ld	hl, 6
	add	hl, sp
	ld	e, (hl)
	dec	hl
	ld	a, (hl)

	rla	; extract LSB of the exponent
	ld	a, e
	rla	; A = exponent

	sub	a, 127	; exponent bias
	jr	c, __truncf_ret_zero
	dec	hl
	dec	hl
	ld	bc, (hl)

	require	_truncf.hijack_modff

	section	.text

	public	_truncf.hijack_modff
_truncf.hijack_modff:
	sub	a, 24	; bits in the mantissa
	sbc	hl, hl	; Carry preserved
	jr	nc, .ret_self
	; HL is -1 here
	cpl
	ld	d, c	; store C
	ld	c, a
	call	__ishl
	ld	c, d	; restore C
	jp	__iand

.ret_self:
	; HL is 0 here
	; already (large) integer, inf, or NaN
	add	hl, bc
	ret

	extern	__ishl
	extern	__iand
