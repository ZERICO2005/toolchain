	assume	adl=1

	section	.text

	public	_truncf, _trunc

_trunc:
_truncf:
	ld	hl, 6
	add	hl, sp
	ld	e, (hl)
	dec	hl
	ld	a, (hl)
	rla	; extract LSB of the exponent
	ld	a, e
	rla	; A = exponent

	sub	a, 127	; exponent bias
	jr	c, .ret_zero

	; load the mantissa into HL and BC
	dec	hl
	dec	hl
	ld	bc, (hl)
	sbc	hl, hl
	add	hl, bc

	sub	a, 23	; bits in the mantissa
	ret	nc	; already (large) integer, inf, or NaN

	sbc	hl, hl	; HL = 0xFFFFFF
	neg
	ld	c, a
	call	__ishl
	jp	__iand

.ret_zero:
	; return signed zero
	ld	a, e
	and	a, $80
	ld	e, a
	sbc	hl, hl
	ret

	extern	__ishl
	extern	__iand
