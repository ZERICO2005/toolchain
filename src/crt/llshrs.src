	assume	adl=1

	section	.text

	public	__llshrs

__llshrs:
; Suboptimal for large shift amounts
	push	af
	push	iy
	ld	iy, 0
	add	iy, sp
	ld	a, (iy + 9)
	cp	a, 48
	jr	nc, .large_shift
	or	a, a
	jr	z, .finish
	push	de
	push	hl
.loop:
	sra	b
	public	__llshrs.hijack_llshru
.hijack_llshru:
	rr	c
	rr	(iy - 1)
	rr	d
	rr	e
	rr	(iy - 4)
	rr	h
	rr	l
	dec	a
	jr	nz, .loop

	ld	(iy - 3), e
	ld	(iy - 2), d
	ex	de, hl
	pop	hl
	ld	l, e
	ld	h, d
	pop	de
	public	__llshrs.finish
.finish:
	pop	iy
	pop	af
	ret

.large_shift:
	sub	a, 48
	ld	h, b
	ld	l, c
	ld	c, a	; C = shift
	call	__sshrs
	sla	b
	; sign extend
	ex	de, hl
	sbc	hl, hl
	ld	h, d
	ld	l, e
	ex	de, hl
	sbc	hl, hl
	ex	de, hl
	ld	b, e	; ld b, sign_extend
	ld	c, e	; ld c, sign_extend
	pop	iy
	pop	af
	ret
