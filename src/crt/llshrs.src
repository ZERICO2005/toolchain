	assume	adl=1

	section	.text

	public	__llshrs

	private	__llshrs_large_shift
__llshrs_large_shift:
	sub	a, 48
	ld	h, b
	ld	l, c
	ld	c, a	; C = shift
	call	__sshrs
	sla	b
	; sign extend
	ex	de, hl
	sbc	hl, hl
	ld	h, d
	ld	l, e
	ex	de, hl
	sbc	hl, hl
	ex	de, hl
	ld	b, e	; ld b, sign_extend
	ld	c, e	; ld c, sign_extend
.finish:
	pop	iy
	pop	af
	ret
__llshrs:
; Suboptimal for large shift amounts
	push	af
	push	iy
	ld	iy, 0
	add	iy, sp
	ld	a, (iy + 9)
	cp	a, 48
	jr	nc, __llshrs_large_shift
	or	a, a
	jr	z, __llshrs_large_shift.finish
	push	de
	push	hl

	require	__llshr_common

	section	.text

	private	__llshr_common
__llshr_common:
.loop:
	sra	b
.llshru_hijack:
	rr	c
	rr	(iy - 1)
	rr	d
	rr	e
	rr	(iy - 4)
	rr	h
	rr	l
	dec	a
	jr	nz, .loop

	ld	(iy - 3), e
	ld	(iy - 2), d
	ex	de, hl
	pop	hl
	ld	l, e
	ld	h, d
	pop	de
	pop	iy
	pop	af
	ret

	section	.text

	public	__llshru
__llshru:
; Suboptimal for large shift amounts
	push	af
	push	iy
	ld	iy, 0
	add	iy, sp
	ld	a, (iy + 9)
	cp	a, 48
	jr	nc, .large_shift
	or	a, a
	jr	z, .finish
	push	de
	push	hl
	srl	b
	jr	__llshr_common.llshru_hijack

.large_shift:
	sub	a, 48
	ld	d, b
	ld	e, c
	ld	c, a	; C = shift
	sbc	hl, hl
	ex	de, hl
	call	__sshru
	; zero extend
	add.s	hl, de
	ld	b, e	; ld b, 0
	ld	c, e	; ld c, 0
.finish:
	pop	iy
	pop	af
	ret

	extern	__sshrs
	extern	__sshru
