	assume	adl=1

	section	.text

	public	__i48mulhs

; UDE:UHL = ((int96_t)UDE:UHL * (int96_t)UIY:UBC) >> 48
__i48mulhs:
	push	af
	push	iy
	push	bc

	; UIY:UBC = abs(UIY:UBC)
	push	hl
	lea	hl, iy
	add	hl, hl
	sbc	a, a
	call	c, __uiyubcNeg

	; UDE:UHL = abs(UDE:UHL)
	ld	hl, $800000
	add	hl, de
	pop	hl
	rla
	rrca
	call	c, __i48neg

	call	__i48mulhu

	; negate if the result should be negative
	add	a, a
	call	pe, __i48neg

	pop	bc
	pop	iy
	pop	af
	ret

	extern	__i48mulhu
	extern	__i48neg
	extern	__uiyubcNeg
