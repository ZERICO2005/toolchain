	assume	adl=1

	section	.text

	public	__llshru
__llshru:
; Suboptimal for large shift amounts
	push	af
	push	iy
	ld	iy, 0
	add	iy, sp
	ld	a, (iy + 9)
	cp	a, 48
	jr	nc, .large_shift
	or	a, a
	jr	z, .finish
	push	de
	push	hl
	srl	b
	jr	.begin

	public	__llshru.hijack_llshrs
.hijack_llshrs:
	push	de
	push	hl
.loop:
	sra	b
.begin:
	rr	c
	rr	(iy - 1)
	rr	d
	rr	e
	rr	(iy - 4)
	rr	h
	rr	l
	dec	a
	jr	nz, .loop

	ld	(iy - 3), e
	ld	(iy - 2), d
	ex	de, hl
	pop	hl
	ld	l, e
	ld	h, d
	pop	de
.finish:
	pop	iy
	pop	af
	ret

.large_shift:
	sub	a, 48
	ld	h, b
	ld	l, c
	ld	c, a	; C = shift
	call	__sshru
	; zero extend
	or	a, a
	ex.s	de, hl
	sbc	hl, hl
	ex	de, hl
	ld	b, e	; ld b, 0
	ld	c, e	; ld c, 0
	pop	iy
	pop	af
	ret

	extern	__sshru
